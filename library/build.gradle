/*
 * Copyright (C) 2016 Alessandro Yuichi Okimoto
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Contact email: alessandro@alessandro.jp
 */
apply plugin: 'com.android.library'

apply from: rootProject.file('gradle/checkstyle.gradle')
apply from: rootProject.file('gradle/jacoco.gradle')

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        multiDexEnabled true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    packagingOptions {
//        exclude 'mockito-extensions/org.mockito.plugins.MockMaker'
//        exclude 'mockito-extensions/org.mockito.plugins.StackTraceCleanerProvider'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Testing
    testCompile deps.supportAnnotations
    testCompile deps.mockito
    testCompile deps.junit
    testCompile deps.assertj3
    testCompile deps.robolectric
    testCompile deps.testRunner
    testCompile deps.testRules
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
        return;
    }
    def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
//    task.archiveName = "${POM_ARTIFACT_ID}-${VERSION_NAME}.jar"
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

apply from: rootProject.file('gradle/bintray_push.gradle')